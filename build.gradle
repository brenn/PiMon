apply plugin: 'java'

group = 'com.ezee'

configurations {
    sshAntTask
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.pi4j:pi4j-core:1.0'
    sshAntTask 'org.apache.ant:ant-jsch:1.7.1', 'jsch:jsch:0.1.29'
}


jar {
    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/MANIFEST.MF'
    }

    manifest {
        attributes (
            "Implementation-Title": "PiMon"
            , "Implementation-Version": version
            , "Main-Class": "no.ezee.pi.Main"
        )
    }

}

task update(dependsOn: 'jar') {
    description = 'Copying jar to raspberrypi'

    ant.taskdef(
            name: 'scp',
            classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
            classpath: configurations.sshAntTask.asPath
    )

    doLast {
        ant.scp(
                todir: 'pi@pi:./bin',
                keyfile: '${user.home}/.ssh/id_rsa',
                passphrase: 'raspberry',
                verbose: true) {
            fileset(dir: 'build/libs') {
                include(name : '*.jar')
            }
        }
    }

}
